; sync.clsp by yakuhito
;; Syncs reward distribution for the current epoch

(mod (
    (
        Ephemeral_State . ; not used
        (@ Current_State
            (
                total_reserves
                active_shares
                (cumulative_payout . remaining_rewards)
                (@ Round_Time_Info (last_update . epoch_end))
            ) 
        ) ; Truth
    )
    (
        update_time
    )
)
    (include condition_codes.clib)

    (defun calculate_new_distribution (active_shares cumulative_payout remaining_rewards cumulative_payout_delta)
        (c
            (+ cumulative_payout cumulative_payout_delta) ; new cumulative_payout
            (- remaining_rewards (* cumulative_payout_delta active_shares)) ; new remaining rewards
        )
    )

    (if (all (not (> update_time epoch_end)) (> update_time last_update))
        (c
            (list
                Ephemeral_State ; new ephemeral state
                total_reserves
                active_shares
                (calculate_new_distribution
                    active_shares
                    cumulative_payout
                    remaining_rewards
                    (if (> active_shares 0)
                        (/ (* remaining_rewards (- update_time last_update)) (* active_shares (- epoch_end last_update)))
                        ; else
                        0
                    ) ; cumulative_payout_delta
                )
                (c update_time epoch_end)
            ) ; new state    
            (list
                (list ASSERT_SECONDS_ABSOLUTE update_time)
                (list
                    CREATE_PUZZLE_ANNOUNCEMENT
                    (concat 's'
                        (sha256 2 
                            (sha256 1 update_time)
                            (sha256 1 epoch_end)
                        ) ; (sha256tree (update_time . epoch_end))
                    ) 
                )
            ) ; conditions
        )
        ; else
        (x)
    )
)