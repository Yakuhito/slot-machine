; add_mirror.clsp by yakuhito
;; Adds a mirror to the reward distributor. The mirror starts accumulating rewards right away.

(mod (
    SINGLETON_MOD_HASH
    VALIDATOR_SINGLETON_STRUCT_HASH ; (sha256 SINGLETON_STRUCT) for validator singleton
    MIRROR_SLOT_1ST_CURRY_HASH ; after 1st curry
    (total_reserves active_shares reward_info current_round_info) ; Current_State
    (
        validator_singleton_inner_puzzle_hash
        mirror_payout_puzzle_hash .
        mirror_shares
    )
)
    (include condition_codes.clib)
    (include curry.clib)
    (include slots.clib)

    (c
        (list
            total_reserves
            (+ active_shares mirror_shares)
            reward_info
            current_round_info
        ) ; new state  
        (list
            ; validator actually wants us to add this mirror
            (list RECEIVE_MESSAGE
                18 ; puzzle hash - puzzle hash
                (sha256 2
                    (sha256 1 mirror_payout_puzzle_hash)
                    (sha256 1 mirror_shares)
                ) ; message = (sha265tree (mirror_payout_puzzle_hash . mirror_shares))
                (curry_hashes_inline SINGLETON_MOD_HASH
                    VALIDATOR_SINGLETON_STRUCT_HASH
                    validator_singleton_inner_puzzle_hash
                )
            )

            (create_slot MIRROR_SLOT_1ST_CURRY_HASH
                (sha256 2
                    (sha256 1 mirror_payout_puzzle_hash)
                    (sha256 2
                        (sha256 1 (f reward_info))
                        (sha256 1 mirror_shares)
                    )
                ) ; slot value = (payout_puzzle_hash initial_cumulative_payout . shares)
            )
        ) ; conditions
    )
        
)