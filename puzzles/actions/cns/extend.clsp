; extend.clsp by yakuhito
;; Used to extend the registration of a name

;; Remember that the slot format is (value (left_value right_value) expiration version . launcher_id)
;;  but we're only interested in 'expiration' (and name to get correct price factor based on length)

(mod (
    OFFER_MOD_HASH
    PAYOUT_PUZZLE_HASH
    SLOT_1ST_CURRY_HASH ; after 1st curry
    Current_State ; Truth
    (renew_amount name neighbors_hash expiration . rest_hash) ; rest_hash is (sha256tree (c version launcher_id))
)
    (include condition_codes.clib)
    (include sha256tree.clib)
    (include cns_price.clib)
    (include curry.clib)
    (include slots.clib)

    (defun get_slot_value_hash (name neighbors_hash expiration rest_hash)
        (sha256 2
            (sha256 1 (sha256 1 name))
            (sha256 2
                neighbors_hash
                (sha256 2
                    (sha256 1 expiration)
                    rest_hash
                )
            )
        )
    )

    (c
        Current_State ; new state
        (list
            ; spend slot
            (spend_slot SLOT_1ST_CURRY_HASH 
                (get_slot_value_hash name neighbors_hash expiration rest_hash)
            )

            ; announcement
            (list CREATE_PUZZLE_ANNOUNCEMENT (concat 'e' (sha256 2 (sha256 1 renew_amount) (sha256 1 name))))
            
            ; create new slot
            (create_slot_with_hint SLOT_1ST_CURRY_HASH
                (get_slot_value_hash
                    name
                    neighbors_hash
                    (+ expiration (* 60 60 24 366 (check_years (/ renew_amount (* (f Current_State) (get_price_factor (strlen name)))))))
                    rest_hash
                )
                (sha256 name)
            ) 
            
            ; assert payment
            (list
                ASSERT_PUZZLE_ANNOUNCEMENT
                (sha256
                    OFFER_MOD_HASH
                    (sha256tree 
                        (list
                            (sha256tree (c name expiration)) ; nonce
                            (list PAYOUT_PUZZLE_HASH renew_amount) ; notarized payment
                        )
                    )
                )
            )
        ) ; conditions
    )
)