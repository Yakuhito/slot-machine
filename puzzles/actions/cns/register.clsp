; register.clsp by yakuhito
;; Responsible for registering new names into the name registry (CNS)

;; Unlike CATalog slot's value is (value (left_value . right_value) expiration version . launcher_id) where:
;;  value is the slot 'key'/id and is defined as (sha256 1 name)
;;  expiration is a timestamp that says when the name will expire
;;  left_value/right_value are the key's of the slot's neighbors
;;  version is used for versioning the 'launcher_id' attribute - for now, the only valid value is 1
;;  launcher_id is, for version 1, a 32-byte value that specifies the name NFT's launcher id
;;      in the future, launcher_id might point to a namespace/sub-registry, for example

;; Also note that anyone can extend a name registration, but only the name NFT may update the version/launcher_id data

(mod (
  PRECOMMIT_1ST_CURRY_HASH ; after 1st curry
  SLOT_1ST_CURRY_HASH ; after 1st curry
  Current_State ; Truth
  (name_hash name_reveal left_value right_value . solution) ; see the 'main' function for rest of arguments 
)
  (include condition_codes.clib)
  (include cns_price.clib)
  (include curry.clib)
  (include slots.clib)

  (defun get_slot_value_hash (value_hash left_value_hash right_value_hash data_hash)
    (sha256 2
        value_hash
        (sha256 2
            (sha256 2 left_value_hash right_value_hash)
            data_hash
        )
    )
  )

  (defun main (
    PRECOMMIT_1ST_CURRY_HASH SLOT_1ST_CURRY_HASH
    Current_State
    ( 
        ; start 'also included in the pre-commit coin'
        name_nft_launcher_id
        version
        start_time
        secret_hash
        ; end
        precommitment_amount
        left_left_value_hash left_data_hash ; left slot info
        right_right_value_hash right_data_hash ; right slot info
    )
    name_hash
    value_hash
    left_value_hash
    right_value_hash
    price
  )
    (c
        Current_State ; new state
        (list
            (list ASSERT_SECONDS_ABSOLUTE start_time)
            
            ; spend left slot
            (spend_slot SLOT_1ST_CURRY_HASH 
                (get_slot_value_hash left_value_hash left_left_value_hash right_value_hash left_data_hash)
            )
            ; spend right slot
            (spend_slot SLOT_1ST_CURRY_HASH
                (get_slot_value_hash right_value_hash left_value_hash right_right_value_hash right_data_hash)
            )
            
            ; create new slot
            (create_slot_with_hint SLOT_1ST_CURRY_HASH
                (get_slot_value_hash
                    value_hash
                    left_value_hash
                    right_value_hash
                    (sha256 2 
                        (sha256 1 (+ start_time (* 60 60 24 366 (check_years (/ precommitment_amount price)))))
                        (sha256 2
                            (sha256 1 version)
                            (sha256 1 name_nft_launcher_id)
                        )
                    ) ; sha256tree data
                )
                value_hash
            )

            ; create new left slot
            (create_slot_with_hint SLOT_1ST_CURRY_HASH 
                (get_slot_value_hash left_value_hash left_left_value_hash value_hash left_data_hash)
                left_value_hash
            ) 
            ; create new right slot
            (create_slot_with_hint SLOT_1ST_CURRY_HASH
                (get_slot_value_hash right_value_hash value_hash right_right_value_hash right_data_hash)
                right_value_hash
            ) 
            
            ; consume precommit coin
            (list
                SEND_MESSAGE
                18 ; puzzle-puzzle
                precommitment_amount
                (curry_hashes PRECOMMIT_1ST_CURRY_HASH
                    (sha256 2
                        (sha256 2 
                            secret_hash
                            name_hash
                        )
                        (sha256 2
                            (sha256 2
                                (sha256 1 version)
                                (sha256 1 name_nft_launcher_id)
                            )
                            (sha256 1 start_time)
                        )
                    ) ; data in precommit coin reveal will be (c (c secret name) (c (c version name_nft_launcher_id) start_time)))
                )
            )
        ) ; conditions
    )
  )

  (defun validate_char (ch)
    (any
        (all (> ch (- "a" 1)) (> (+ "z" 1) ch))
        (all (> ch (- "0" 1)) (> (+ "9" 1) ch))
    )
  )

  (defun strlen_and_validate (name)
    (if name
        (if (validate_char (substr name 0 1))
            (+ 1 (strlen_and_validate (substr name 1)))
            ; else 
            (x)
        )
        ; else
        ()
    )
  )

  (if (all (= name_hash (sha256 1 name_reveal)) (> name_hash left_value) (> right_value name_hash))
    (main
            PRECOMMIT_1ST_CURRY_HASH SLOT_1ST_CURRY_HASH
            Current_State
            solution
            name_hash
            (sha256 1 name_hash)
            (sha256 1 left_value) 
            (sha256 1 right_value) 
            (*
                (get_price_factor (strlen_and_validate name_reveal)) 
                (f Current_State)
            )
        )
        ; else
        (x)
  )
)