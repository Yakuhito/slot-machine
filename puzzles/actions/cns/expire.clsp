; expire.clsp by yakuhito
;; Used to expire (i.e., remove) a name from the system

;; Remember that the slot format is (value (left_value right_value) expiration version . launcher_id)

(mod (
    SLOT_1ST_CURRY_HASH ; after 1st curry
    Current_State ; Truth
    (
        value
        left_value left_left_value left_rest_hash
        right_value right_right_value right_rest_hash
        expiration
        data_hash ; (sha256tree (c version launcher_id))
    )
)
    (include condition_codes.clib)
    (include sha256tree.clib)
    (include cns_price.clib)
    (include curry.clib)
    (include slots.clib)

    (defun get_slot_value_hash (value left_value right_value rest_hash)
        (sha256 2
            (sha256 1 value)
            (sha256 2
                (sha256 2
                    (sha256 1 left_value)
                    (sha256 1 right_value)
                )
                rest_hash
            )
        )
    )
        
    (c
        Current_State ; new state
        (list
            ; assert it really expired
            (list ASSERT_SECONDS_ABSOLUTE expiration)

            ; spend main slot
            (spend_slot SLOT_1ST_CURRY_HASH 
                (get_slot_value_hash value left_value right_value (sha256 2  (sha256 1 expiration) data_hash))
            )

            ; spend left slot
            (spend_slot SLOT_1ST_CURRY_HASH 
                (get_slot_value_hash left_value left_left_value value left_rest_hash)
            )
            
            ; spend right slot
            (spend_slot SLOT_1ST_CURRY_HASH 
                (get_slot_value_hash right_value value right_right_value right_rest_hash)
            )
            
            ; create new left slot
            (create_slot_with_hint SLOT_1ST_CURRY_HASH
                (get_slot_value_hash left_value left_left_value right_value left_rest_hash)
                (sha256 1 left_value)
            ) 
            
            ; create new right slot
            (create_slot_with_hint SLOT_1ST_CURRY_HASH
                (get_slot_value_hash right_value left_value right_right_value right_rest_hash)
                (sha256 1 right_value)
            ) 
        ) ; conditions
    )
)