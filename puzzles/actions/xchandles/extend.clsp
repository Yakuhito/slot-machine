; extend.clsp by yakuhito
;; Used to extend the registration of a handle

;; Remember that the slot format is (c (c handle_hash (c left right)) (c expiration (c owner resolved)))
;;  but we're only modifying 'expiration' (and using handle to get correct price factor based on length)

(mod (
    OFFER_MOD_HASH
    PAYOUT_PUZZLE_HASH
    SLOT_1ST_CURRY_HASH ; after 1st curry
    (@ Truth
        (
            Ephemeral_State .
            (@
                Current_State
                (Cat_Maker_Puzzle_Hash Pricing_Puzzle_Hash . Expired_Handle_Pricing_Puzzle_Hash)
            )
        )
    )
    (
        pricing_puzzle_reveal
        (@ pricing_solution (expiration handle . rest))
        cat_maker_puzzle_reveal
        cat_maker_solution
        neighbors . 
        rest_data
    )
)
    (include condition_codes.clib)
    (include sha256tree.clib)
    (include xchandles.clib)
    (include curry.clib)
    (include slots.clib)

    (defun-inline main ((total_price . registered_time))
        (c
            Truth
            (list
                ; spend slot
                (spend_slot_inline SLOT_1ST_CURRY_HASH 
                    (get_xchandles_slot_value_hash (sha256 1 handle) neighbors expiration rest_data)
                )

                ; announcement
                (list CREATE_PUZZLE_ANNOUNCEMENT (concat 'e' (sha256tree (c total_price handle))))
                
                ; can't extend if expired
                (list ASSERT_BEFORE_SECONDS_ABSOLUTE expiration)

                ; create new slot
                (create_slot_inline SLOT_1ST_CURRY_HASH
                    (get_xchandles_slot_value_hash
                        (sha256 1 handle)
                        neighbors
                        (+ expiration registered_time)
                        rest_data
                    )
                ) 
                
                ; assert payment
                (list
                    ASSERT_PUZZLE_ANNOUNCEMENT
                    (sha256
                        (a 
                            cat_maker_puzzle_reveal
                            (c OFFER_MOD_HASH cat_maker_solution)
                        ) ; full CAT puzzle hash
                        (sha256tree 
                            (list
                                (sha256tree (c handle expiration))
                                (list PAYOUT_PUZZLE_HASH total_price (list PAYOUT_PUZZLE_HASH)) ; notarized payment
                            )
                        )
                    )
                )
            ) ; conditions
        )
    )

    (if (all
        (= Cat_Maker_Puzzle_Hash (sha256tree cat_maker_puzzle_reveal)) ; cat maker puzzle reveal is correct
        (= Pricing_Puzzle_Hash (sha256tree pricing_puzzle_reveal)) ; pricing puzzle reveal is correct
    )
        (main
            (a pricing_puzzle_reveal pricing_solution)
        )
        ; else
        (x)
    )
)