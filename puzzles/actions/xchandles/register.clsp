; register.clsp by yakuhito
;; Responsible for registering new handles into the handle registry (XCHandles)

;; Unlike CATalog slot's value is (value (left_value . right_value) expiration . launcher_id) where:
;;  value is the slot 'key'/id and is defined as (sha256 1 handle)
;;  left_value/right_value are the key's of the slot's neighbors
;;  expiration is a timestamp that says when the handle will expire
;;  launcher_id is a 32-byte value that specifies the handle NFT's launcher id
;;       in the future, launcher_id might point to a namespace/sub-registry, for example

;; Also note that anyone can extend a handle registration, but only the handle NFT may update the launcher_id data

(mod (
  CAT_MOD_HASH
  PRECOMMIT_1ST_CURRY_HASH ; after 1st curry
  SLOT_1ST_CURRY_HASH ; after 1st curry
  PAYOUT_PUZZLE_HASH
  Current_State ; Truth
  (handle_hash handle_reveal left_value right_value . solution) ; see the 'main' function for rest of arguments 
)
  (include condition_codes.clib)
  (include xchandles_price.clib)
  (include curry.clib)
  (include slots.clib)

  (defun get_slot_value_hash (value_hash left_value_hash right_value_hash data_hash)
    (sha256 2
        value_hash
        (sha256 2
            (sha256 2 left_value_hash right_value_hash)
            data_hash
        )
    )
  )

  (defun main (
    CAT_MOD_HASH PRECOMMIT_1ST_CURRY_HASH SLOT_1ST_CURRY_HASH PAYOUT_PUZZLE_HASH
    Current_State
    ( 
        ; start 'also included in the pre-commit coin'
        handle_nft_launcher_id
        start_time
        secret_hash
        refund_puzzle_hash_hash ; needed to compute precommit coin puzzle hash
        ; end
        precommitment_amount
        left_left_value_hash left_data_hash ; left slot info
        right_right_value_hash right_data_hash ; right slot info
    )
    handle_hash
    value_hash
    left_value_hash
    right_value_hash
    price
  )
    (c
        Current_State ; new state
        (list
            (list ASSERT_SECONDS_ABSOLUTE start_time)
            
            ; spend left slot
            (spend_slot SLOT_1ST_CURRY_HASH 
                (get_slot_value_hash left_value_hash left_left_value_hash right_value_hash left_data_hash)
            )
            ; spend right slot
            (spend_slot SLOT_1ST_CURRY_HASH
                (get_slot_value_hash right_value_hash left_value_hash right_right_value_hash right_data_hash)
            )
            
            ; create new slot
            (create_slot SLOT_1ST_CURRY_HASH
                (get_slot_value_hash
                    value_hash
                    left_value_hash
                    right_value_hash
                    (sha256 2 
                        (sha256 1 (+ start_time (* 60 60 24 366 (check_years (/ precommitment_amount price)))))
                        (sha256 1 handle_nft_launcher_id)
                    ) ; sha256tree data
                )
            )

            ; create new left slot
            (create_slot SLOT_1ST_CURRY_HASH 
                (get_slot_value_hash left_value_hash left_left_value_hash value_hash left_data_hash)
            ) 
            ; create new right slot
            (create_slot SLOT_1ST_CURRY_HASH
                (get_slot_value_hash right_value_hash value_hash right_right_value_hash right_data_hash)
            ) 
            
            ; consume precommit coin
            (list
                SEND_MESSAGE
                19 ; sender puzzle + receiver puzzle + receiver amount
                PAYOUT_PUZZLE_HASH ; message
                (curry_hashes CAT_MOD_HASH
                    (sha256 1 CAT_MOD_HASH)
                    (f Current_State) ; first item = hash of payment CAT asset id
                    (curry_hashes PRECOMMIT_1ST_CURRY_HASH
                        refund_puzzle_hash_hash
                        (sha256 2
                            (sha256 2 
                                secret_hash
                                handle_hash
                            )
                            (sha256 2
                                (sha256 1 handle_nft_launcher_id)
                                (sha256 1 start_time)
                            )
                        ) ; data in precommit coin reveal will be (c (c secret handle) (c handle_nft_launcher_id start_time)))
                    )
                )
                precommitment_amount
            )
        ) ; conditions
    )
  )

  (defun validate_char (ch)
    (any
        (all (> ch (- "a" 1)) (> (+ "z" 1) ch))
        (all (> ch (- "0" 1)) (> (+ "9" 1) ch))
    )
  )

  (defun strlen_and_validate (handle)
    (if handle
        (if (validate_char (substr handle 0 1))
            (+ 1 (strlen_and_validate (substr handle 1)))
            ; else 
            (x)
        )
        ; else
        ()
    )
  )

  (if (all (= handle_hash (sha256 1 handle_reveal)) (> handle_hash left_value) (> right_value handle_hash))
    (main
            CAT_MOD_HASH PRECOMMIT_1ST_CURRY_HASH SLOT_1ST_CURRY_HASH PAYOUT_PUZZLE_HASH
            Current_State
            solution
            handle_hash
            (sha256 1 handle_hash)
            (sha256 1 left_value) 
            (sha256 1 right_value) 
            (*
                (get_price_factor (strlen_and_validate handle_reveal)) 
                (r Current_State) ; base price in token mojos
            )
        )
        ; else
        (x)
  )
)