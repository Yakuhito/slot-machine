; oracle.clsp by yakuhito
;; Used to 'confirm' a registration on-chain by creating a puzzle announcement

(mod (
    SLOT_1ST_CURRY_HASH ; after 1st curry
    Current_State ; Truth
    Ephemeral_State ; not used
    (
        handle_hash .
        rest_treehash ; (sha256tree ((left_value . right_value) expiration owner_launcher_id . resolved_launcher_id))
    )
)
    (include condition_codes.clib)
    (include sha256tree.clib)
    (include curry.clib)
    (include slots.clib)

    (defun get_conditions (SLOT_1ST_CURRY_HASH hint data_treehash)
        (list
            ; spend slot
            (spend_slot_inline SLOT_1ST_CURRY_HASH data_treehash)

            ; create new slot
            (create_slot_inline SLOT_1ST_CURRY_HASH data_treehash hint)

            ; announce value
            (list CREATE_PUZZLE_ANNOUNCEMENT (concat 'o' data_treehash))
        )
    )

    (list
        Current_State ; new state
        () ; new ephemeral state
        (get_conditions
            SLOT_1ST_CURRY_HASH
            handle_hash
            (sha256 2 (sha256 1 handle_hash) rest_treehash)
        ) ; conditions
    )
)