; update.clsp by yakuhito
;; Used to update the launcher_id of a record

;; Note: The update is final.

(mod (
    SINGLETON_MOD_HASH
    SINGLETON_LAUNCHER_PUZZLE_HASH_HASH
    SLOT_1ST_CURRY_HASH ; after 1st curry
    Current_State ; Truth
    (
        value_hash
        neighbors_hash ; (sha256tree (left_value . right_value))
        expiration
        current_owner_launcher_id
        current_resolved_launcher_id
        new_owner_launcher_id
        new_resolved_launcher_id .
        announcer_inner_puzzle_hash
    )
)
    (include condition_codes.clib)
    (include curry.clib)
    (include slots.clib)

    (defun get_slot_data_hash (value_hash neighbors_hash expiration owner_launcher_id resolved_launcher_id)
        (sha256 2
            value_hash
            (sha256 2
                neighbors_hash
                (sha256 2
                    (sha256 1 expiration)
                    (sha256 2
                        (sha256 1 owner_launcher_id)
                        (sha256 1 resolved_launcher_id)
                    )
                )
            )
        )
    )

    (c
        Current_State ; new state
        (list
            ; spend slot
            (spend_slot SLOT_1ST_CURRY_HASH 
                (get_slot_data_hash
                    value_hash
                    neighbors_hash
                    expiration
                    current_owner_launcher_id
                    current_resolved_launcher_id
                )
            )

            ; create new slot
            (create_slot SLOT_1ST_CURRY_HASH
                (get_slot_data_hash
                    value_hash
                    neighbors_hash
                    expiration
                    new_owner_launcher_id
                    new_resolved_launcher_id
                )
            )

            ; assert message from current owner
            (list
                RECEIVE_MESSAGE
                18 ; puzzle-puzzle
                (sha256 2
                    value_hash
                    (sha256 2
                        (sha256 1 new_owner_launcher_id)
                        (sha256 1 new_resolved_launcher_id)
                    )
                )
                (curry_hashes SINGLETON_MOD_HASH
                    (sha256 2
                        (sha256 1 SINGLETON_MOD_HASH)
                        (sha256 2
                            (sha256 1 current_owner_launcher_id)
                            SINGLETON_LAUNCHER_PUZZLE_HASH_HASH
                        )
                    ) ; sha256tree of announcer SINGLETON_MOD
                    announcer_inner_puzzle_hash
                )
            )
        )
    )
)