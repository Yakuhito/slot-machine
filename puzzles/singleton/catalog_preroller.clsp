; catalog_preroller.clsp by yakuhito
;; When launched, the catalog singleton is in 'preroll' mode, meaning that it
;; launches initial slots and CAT NFTs
;; This puzzle allows the preroll puzzle to assert that the CAT NFT was properly created

(mod (
    MOD_HASHES ; (LAUNCHER_MOD_HASH . UNIQUENESS_PRELAUNCHER_1ST_CURRY_HASH)
    NFT_INFOS ; (list (eve_nft_full_puzhash . asset_id_hash) ...)
    BASE_CONDITIONS ; generated by the driver code
    my_coin_id ; can't access this while building the puzzle; only when spending the coin
)
    (include condition_codes.clib)
    (include curry.clib)

    (defun assert_nfts (MOD_HASHES my_coin_id nft_infos conditions_so_far)
        (if nft_infos
            (c
                (list
                    ASSERT_CONCURRENT_SPEND
                    (coinid
                        (coinid
                            (coinid
                                my_coin_id 
                                (curry_hashes_inline (r MOD_HASHES) ; UNIQUENESS_PRELAUNCHER_1ST_CURRY_HASH
                                    (r (f nft_infos)) ; (sha256 1 asset_id)
                                )
                                0
                            ) ; uniqueness pre-launcher id
                            (f MOD_HASHES) ; LAUNCHER_MOD_HASH
                            1
                        ) ; launcher id
                        (f (f nft_infos))
                        1
                    )
                )
                (assert_nfts MOD_HASHES my_coin_id (r nft_infos) conditions_so_far)
            )
            ; else
            conditions_so_far
        )
    )

    (c
        (list ASSERT_MY_COIN_ID my_coin_id)
        (assert_nfts MOD_HASHES my_coin_id NFT_INFOS BASE_CONDITIONS)
    )
)